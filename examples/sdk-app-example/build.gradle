buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agpVersion"
        classpath 'com.bugsnag:bugsnag-android-gradle-plugin:3.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    mavenCentral()
    jcenter()
}

android {
    signingConfigs {
        config {
            keyAlias 'password'
            keyPassword 'password'
            storeFile file('../../features/fixtures/fakekeys.jks')
            storePassword 'password'
        }
    }
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.config
        ndk {
            // NOTE: remove armeabi if testing on NDK r17+
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'armeabi', 'x86', 'x86_64'
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            signingConfig signingConfigs.config
        }
    }
    flavorDimensions "regular"
    productFlavors {
        javaExample {
            applicationIdSuffix ".java"
            signingConfig signingConfigs.config
        }
        kotlinExample {
            applicationIdSuffix ".kotlin"
            signingConfig signingConfigs.config
        }
    }
    lintOptions {
        disable 'OnClick'
        abortOnError true
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

// set this value to true if you wish to test with a local artefact
// it will instruct gradle to copy the SO files from the AAR to the correct build directory
def usingLocalArtefact = false

dependencies {
    if (usingLocalArtefact) {
        implementation project(path: ':sdk', configuration: 'default')
        api project(path: ':ndk', configuration: 'default')
    } else {
        implementation 'com.bugsnag:bugsnag-android-ndk:4.12.0'
    }

    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support:support-v4:27.0.0'

    kotlinExampleImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation "com.android.support.test:runner:0.5", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test:rules:0.5", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

apply from: 'checkstyle.gradle'
apply plugin: 'com.bugsnag.android.gradle'

if (usingLocalArtefact) {
    apply from: 'ndk_local_setup.gradle'
}
